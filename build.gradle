import org.ajoberstar.grgit.Grgit

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'org.ajoberstar:grgit:1.7.0'
    }
}

plugins {
    id 'ninja.miserable.blossom' version '1.0.1'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
    id 'java'
}


defaultTasks 'clean', 'build'
sourceCompatibility = 1.8

configurations {
    shade
    compile.extendsFrom(shade)
}
version = "0.2.0-pre"
blossom {
    replaceToken('@spongeVersion@', version)
}

def buildNumber = ""
ext {
    //代码来自插件ebi中的build.gradle → : https://github.com/euOnmyoji/EpicBanItem---Sponge
    //noinspection GrDeprecatedAPIUsage  不会用新版
    git = Grgit.open(file(".git"))
    parents = git.head().parentIds
    index = 0// Offset to match CI
    while (parents != null && !parents.isEmpty()) {
        parents = git.getResolve().toCommit(parents.get(0)).getParentIds()
        index++
    }
    //noinspection GrReassignedInClosureLocalVar
    buildNumber = "${index}"
}

version = "${version}-b${buildNumber}"
group 'com.github.euonmyoji.yysscoreboard'

repositories {
    mavenCentral()
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven'
    }
    maven { url = 'https://jitpack.io' }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

dependencies {
    compileOnly 'org.spongepowered:spongeapi:7.1.0'
    compileOnly 'com.github.rojo8399:PlaceholderAPI:master-SNAPSHOT'
    shade 'org.bstats.bStats-Metrics:bstats-sponge:1.4'
}

shadowJar() {
    configurations = [project.configurations.compile]
    from sourceSets.main.output
    classifier = ""
}

artifacts {
    archives shadowJar
}